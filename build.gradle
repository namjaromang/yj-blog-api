plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id "com.diffplug.spotless" version "5.0.0"
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.7"
    id 'java'
}

group = 'com.blog'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation "org.liquibase:liquibase-core"
    implementation 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4:1.16'

    implementation 'com.graphql-java-kickstart:graphql-spring-boot-starter:7.0.1'
    runtimeOnly 'com.graphql-java-kickstart:altair-spring-boot-starter:7.0.1'

    implementation 'com.auth0:java-jwt:3.8.3'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}


spotless {
    java {
        targetExclude("src/main/generated/**")
        googleJavaFormat("1.7").aosp()

        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()

        importOrder ''
    }
}

task installGitHooks(type: Copy) {
    from new File(rootProject.rootDir, 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

build.dependsOn installGitHooks

idea.project.settings {
    taskTriggers {
        afterSync tasks.getByName("installGitHooks")
    }
}